Section Header

  + name := DEPTH;
  - copyright := "2014 Benjamin Chetioui";
  - author := "Benjamin Chetioui";
  - comment := "File containing functions to apply on points.";

Section Inherit

  - parent_depth:OBJECT := OBJECT;

Section Public

  - nb_objects:INTEGER := 0;

  - solve :(FAST_ARRAY(LINES),INTEGER) <-
  (
    + p1,p2,tmp:POINT;
    + ppmm,focal_length,baseline:REAL_16_16;
    + nb_points:INTEGER;
    + x1,y1,x2,y2:INTEGER;
    + f:FAST_ARRAY(LINES);
    + myl,tmpl:LINES;

    IO.read_line;
    // In mm
    focal_length := IO.last_string.to_real_16_16;
    IO.read_line;
    // Actually in ppmm
    ppmm := IO.last_string.to_real_16_16;
    IO.read_line;
    // In mm
    baseline := IO.last_string.to_real_16_16;
    IO.read_integer;
    nb_points := (IO.last_integer);

    f := FAST_ARRAY(LINES).create_with_capacity(nb_points);

    p1 := POINT.clone;
    p2 := POINT.clone;
    myl := LINES.clone;

    1.to nb_points do { i:INTEGER;
      IO.read_integer;
      x1 := IO.last_integer;
      IO.read_integer;
      y1 := IO.last_integer;
      IO.read_integer;
      x2 := IO.last_integer;
      IO.read_integer;
      y2 := IO.last_integer;
      p1.create(x1-1280,y1-400,0);
      p2.create(x2-1280,y2-400,0);
      (x1 > x2).if {
        p1.depth(p2, baseline, focal_length, ppmm);
        tmp := p1.clone;
      }
      // Points weren't entered as they should have been.
      // Nevertheless, trying to use the data we were given.
      else {
        (x1 = x2).if {
          "Error: division by zero.".println;
          die_with_code (1);
        };
        p2.depth(p1, baseline, focal_length, ppmm);
        tmp := p2.clone;
      };
      myl.create(tmp, nb_points);
      tmpl := myl.clone;
      f.add_last(tmpl);
      nb_objects := nb_objects + 1;
    };
    f,nb_objects
  );

  - atoi (str:STRING) :INTEGER <-
  (
    + result:INTEGER;
    + mult:INTEGER;
    + len:INTEGER;

    len := str.count;
    result := 0;
    mult := 1;

    ((str.item 1) = '-').if {
      mult := -1;
    }
    else {
      result := result + (str.item 1).decimal_value;
    };
    2.to len do { i:INTEGER;
      result := result*10 + (str.item i).decimal_value;
    };
    result*mult
  );

  /*- main <-
  (
    solve;
  );*/
